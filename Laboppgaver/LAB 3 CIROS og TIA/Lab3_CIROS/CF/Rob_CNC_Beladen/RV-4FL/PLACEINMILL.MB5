1 '##########################################################'
2 '### ProgramName:
3 '###  - PlaceInMill
4 '###
5 '### ProgramDescription:
6 '### The robot loads the mill machine with the workpiece
7 '###
8 '### Modifications:
9 '###  - 26.08.2016		Puneeth Rajendra		Created
10 '###
11 '### Process of Operation:
12 '### The robot places the workpiece inside the mill
13 '##########################################################'
14 '
15 '##########################################################'
16 '		Decleration Of Input parameters for the Program
17 '##########################################################'
18 Def Pos PlacePose
19 '
20 '##########################################################'
21 '		Decleration Of Input and Output signals
22 '##########################################################'
23 '
24 '>>>>>> INPUTS - Robot Controller <<<<<<<<'
25 Def Io DoorOpened = Bit,16   'CNC door is opened
26 Def Io ViceOpened = Bit,18   'CNC vice is opened
27 Def Io PartGripped  = Bit,901      'Work piece gripped by the robot
28 Def Io FullyClosed  = Bit,902      'Gripper fully closed
29 '
30 '>>>>>> INPUTS - ProfiNet <<<<<<<<'
31 Def Io DI_RetryLastStep = Bit,132
32 Def Io DI_ExitCurrCycle = Bit,133
33 '
34 '>>>>>> OUTPUTS - ProfiNet <<<<<<<<'
35 Def Io ReturnErrorValue = Byte,164 'Return value of Error to the PLC
36 '
37 '##########################################################'
38 '		Decleration Of Local Variables
39 '##########################################################'
40 Def Inte FunErrorStatus
41 '
42 '###########################################################'
43 '       Program
44 '###########################################################'
45 *RetryStep:
46 '>>>>>> Initialize the values <<<<<<<<<<'
47  FunErrorStatus% = 0
48  ReturnErrorValue = 0
49  Spd m_Fast
50  Ovrd m_ProgOvrd
51  If ((DI_RetryLastStep = 1) Or (DI_ExitCurrCycle = 1)) GoTo *RetryStep
52  '
53  '>>>>>> Check if CNC is prepared <<<<<<<<<<'
54  If DoorOpened = 0 Then
55   FunErrorStatus% = M_CNCRdyErr
56  EndIf
57  If (FunErrorStatus% > 0) GoTo *ErrorLable
58  '
59  If ViceOpened = 0 Then
60   FunErrorStatus% = M_CNCRdyErr
61  EndIf
62  If (FunErrorStatus% > 0) GoTo *ErrorLable
63  '
64  '>>>>>> Move close to CNC position <<<<<<<<<<'
65  Cnt 1, 20,20
66  Mov P_HelpLoadPose1
67  Mov P_HelpLoadPose2
68  Cnt 0
69  '
70  '>>>>>> Calculate the place pose <<<<<<<<<<'
71  PlacePose = P_MillClampPose
72  '
73  '>>>>>> Place inside the mill <<<<<<<<<<'
74  Mvs PlacePose, -80
75  Spd m_Slow
76  Mvs PlacePose
77  Dly 0.5
78  '
79  '>>>>>> Clamp the workpiece in turn machine <<<<<<<<<<'
80  CallP "CNCINTFACE16","CloseVice"
81  FunErrorStatus% = m_ReturnedError
82  If (m_ReturnedError > 0) GoTo *EndOfProg
83  CallP "GrpOpen"
84  '
85  '>>>>>> Come out of mill machine <<<<<<<<<<'
86  Spd m_Fast
87  Mvs PlacePose, -80
88  Mvs P_HelpLoadPose2
89  '
90  '>>>>>> Return if no error found <<<<<<<<<<'
91  GoTo *EndOfProg
92  '
93 *ErrorLable:
94  ReturnErrorValue = FunErrorStatus%
95  If ((DI_RetryLastStep = 0) And (DI_ExitCurrCycle = 0)) GoTo *ErrorLable
96  If DI_RetryLastStep = 1 GoTo *RetryStep
97  If DI_ExitCurrCycle = 1 Then
98   ReturnErrorValue = 0
99  EndIf
100 *EndOfProg:
101  m_ReturnedError = FunErrorStatus%
102 