1 '##########################################################'
2 '### ProgramName:
3 '###  - GetCamResult
4 '###
5 '### Function Input Parameters:
6 '###  - CamPrgNumber
7 '###
8 '### ProgramDescription:
9 '### Open the communication channel to camera
10 '### Select the camera program
11 '### Make an image
12 '### Read the result
13 '###
14 '### Modifications:
15 '###  - 27.07.2016		Puneeth Rajendra		Created
16 '###
17 '##########################################################'
18 '
19 '##########################################################'
20 '		Decleration Of Input parameters for the Program
21 '##########################################################'
22 Def Inte CamPrgNumber 'Camera program no sent by the main program
23 FPrm CamPrgNumber%
24 '
25 '##########################################################'
26 '		Decleration Of Input and Output signals
27 '##########################################################'
28 '>>>>>> OUTPUTS - Robot Controller <<<<<<<<'
29 Def Io DO_MountLock = Bit,16
30 '
31 '>>>>>> INPUTS - ProfiNet <<<<<<<<'
32 Def Io DI_RetryLastStep = Bit,132
33 Def Io DI_ExitCurrCycle = Bit,133
34 '
35 '>>>>>> OUTPUTS - ProfiNet <<<<<<<<'
36 Def Io ReturnErrorValue = Byte,164 'Return value of Error to the PLC
37 '
38 '##########################################################'
39 '		Decleration Of Local Variables
40 '##########################################################'
41 Def Inte FunErrorStatus
42 Def Inte XPose, YPose, Angle
43 Def Char SendStrToCam
44 Def Char ConnectStr, OnConnectStr
45 Def Char ChangePrgStr, OnChangePrgStr
46 Def Char MakeImageStr, OnMakeImageStr
47 '
48 '###########################################################'
49 '       Program
50 '###########################################################'
51 '>>>>>> Initialize the values <<<<<<<<<<'
52 OnConnectStr$ = "20 SBOx-Q-ZEWAS: no authentication required."
53 OnChangePrgStr$ = "1 OK: operation successful."
54 OnMakeImageStr$ = "1 OK: operation successful."
55 XPose% = -9999
56 YPose% = -9999
57 Angle% = -9999
58 '
59 *RetryStep:
60  FunErrorStatus% = 0
61  ReturnErrorValue = 0
62  If ((DI_RetryLastStep = 1) Or (DI_ExitCurrCycle = 1)) GoTo *RetryStep
63  '>>>>>> Open the communication channel to camera <<<<<<<<<<'
64  Close #1
65  Dly 2
66  Open "COM3:" As #1
67  Input #1, ConnectStr$
68  If (ConnectStr$ <> OnConnectStr$) Then
69   FunErrorStatus% = m_CameraError
70  EndIf
71  If (FunErrorStatus% > 0) GoTo *ErrorLable
72  '
73  '>>>>>> Select the camera program <<<<<<<<<<'
74  SendStrToCam$ = "CHANGEPRG " + Str$(CamPrgNumber%)
75  Print #1,SendStrToCam$
76  Input #1,ChangePrgStr$
77  If (ChangePrgStr$ <> OnChangePrgStr$) Then
78   FunErrorStatus% = m_CameraError
79  EndIf
80  If (FunErrorStatus% > 0) GoTo *ErrorLable
81  '
82  '>>>>>> Make an image <<<<<<<<<<'
83  SendStrToCam$ = "IMAGE"
84  Print #1,SendStrToCam$
85  Input #1,MakeImageStr$
86  If (MakeImageStr$ <> OnMakeImageStr$) Then
87   FunErrorStatus% = m_CameraError
88  EndIf
89  If (FunErrorStatus% > 0) GoTo *ErrorLable
90  '
91  '>>>>>> Read the result <<<<<<<<<<'
92  SendStrToCam$ = "RNV [X]"
93  Print #1,SendStrToCam$
94  Input #1,XPose%
95  SendStrToCam$ = "RNV [Y]"
96  Print #1,SendStrToCam$
97  Input #1,YPose%
98  SendStrToCam$ = "RNV [Angle]"
99  Print #1,SendStrToCam$
100  Input #1,Angle%
101  If ((XPose% < m_XminCamField) Or (XPose% > m_XmaxCamField)) Then
102   FunErrorStatus% = m_CameraError
103  EndIf
104  If (FunErrorStatus% > 0) GoTo *ErrorLable
105  If ((YPose% < m_YminCamField) Or (YPose% > m_YmaxCamField)) Then
106   FunErrorStatus% = m_CameraError
107  EndIf
108  If (FunErrorStatus% > 0) GoTo *ErrorLable
109  If ((Angle% < -360) Or (Angle% > 360)) Then
110   FunErrorStatus% = m_CameraError
111  EndIf
112  If (FunErrorStatus% > 0) GoTo *ErrorLable
113  P_VisionNew = P_Vision
114  P_VisionNew.X = XPose%
115  P_VisionNew.Y = YPose%
116  P_VisionNew.C = Rad(Angle%)
117  GoTo *EndOfProg
118 '
119 *ErrorLable:
120  ReturnErrorValue = FunErrorStatus%
121  If ((DI_RetryLastStep = 0) And (DI_ExitCurrCycle = 0)) GoTo *ErrorLable
122  If DI_RetryLastStep = 1 GoTo *RetryStep
123  If DI_ExitCurrCycle = 1 Then
124   ReturnErrorValue = 0
125   DO_MountLock = 0
126  EndIf
127 *EndOfProg:
128  m_ReturnedError = FunErrorStatus%
129 