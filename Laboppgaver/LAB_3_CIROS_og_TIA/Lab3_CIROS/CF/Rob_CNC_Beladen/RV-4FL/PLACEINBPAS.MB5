1 '##########################################################'
2 '### ProgramName:
3 '###  - PlaceInBPas
4 '###
5 '### ProgramDescription:
6 '### The robot places the workpiece inside the ByPass
7 '###
8 '### Modifications:
9 '###  - 26.08.2016		Puneeth Rajendra		Created
10 '###
11 '### Process of Operation:
12 '### The robot places the workpiece inside the By pass
13 '##########################################################'
14 '
15 '##########################################################'
16 '		Decleration Of Input parameters for the Program
17 '##########################################################'
18 Def Pos PlacePose
19 '
20 '##########################################################'
21 '		Decleration Of Input and Output signals
22 '##########################################################'
23 '>>>>>> INPUTS - ProfiNet <<<<<<<<'
24 Def Io DI_WPPalAvail = Bit,244   'Workpiece pallet is available
25 Def Io DI_WPAtStopAvail = Bit,245  'Workpiece is present at the stopper position
26 Def Io DI_RetryLastStep = Bit,132
27 Def Io DI_ExitCurrCycle = Bit,133
28 '
29 '>>>>>> OUTPUTS - ProfiNet <<<<<<<<'
30 Def Io ReturnErrorValue = Byte,164 'Return value of Error to the PLC
31 '
32 '##########################################################'
33 '		Decleration Of Local Variables
34 '##########################################################'
35 Def Inte FunErrorStatus
36 '
37 '###########################################################'
38 '       Program
39 '###########################################################'
40 Ovrd m_ProgOvrd
41 Cnt 1
42 Jovrd 50
43 Mov P_Home
44 Cnt 0
45 *RetryStep:
46 '>>>>>> Initialize the values <<<<<<<<<<'
47  FunErrorStatus% = 0
48  ReturnErrorValue = 0
49  Spd m_Fast
50  If ((DI_RetryLastStep = 1) Or (DI_ExitCurrCycle = 1)) GoTo *RetryStep
51  '
52  '>>>>>> Validate ByPass sensors <<<<<<<<'
53  If DI_WPAtStopAvail = 1 Then
54   FunErrorStatus% = m_ByPassPosOcc
55  EndIf
56  If (FunErrorStatus% > 0) GoTo *ErrorLable
57  '
58  '>>>>>> Place inside the box <<<<<<<<<<'
59  PlacePose = P_ByPassPalPose
60  Mov PlacePose, -100
61  Spd m_Slow
62  Mvs PlacePose
63  Dly 0.5
64  CallP "GrpOpen"
65  Spd m_Fast
66  Mvs PlacePose, -100
67  Mov P_Home
68  '
69  '>>>>>> Return if no error found <<<<<<<<<<'
70  GoTo *EndOfProg
71  '
72 *ErrorLable:
73  ReturnErrorValue = FunErrorStatus%
74  If ((DI_RetryLastStep = 0) And (DI_ExitCurrCycle = 0)) GoTo *ErrorLable
75  If DI_RetryLastStep = 1 GoTo *RetryStep
76  If DI_ExitCurrCycle = 1 Then
77   ReturnErrorValue = 0
78  EndIf
79 *EndOfProg:
80  m_ReturnedError = FunErrorStatus%
81 