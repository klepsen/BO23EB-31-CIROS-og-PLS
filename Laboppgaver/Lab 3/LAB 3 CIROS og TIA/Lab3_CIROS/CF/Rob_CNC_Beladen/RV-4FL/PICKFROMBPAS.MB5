1 '##########################################################'
2 '### ProgramName:
3 '###  - PickFromBPas
4 '###
5 '### ProgramDescription:
6 '### The robot picks the workpiece from the	Bypass
7 '###
8 '### Modifications:
9 '###  - 26.08.2016		Puneeth Rajendra		Created
10 '###
11 '### Process of Operation:
12 '### The robot picks from the source position
13 '##########################################################'
14 '
15 '##########################################################'
16 '		Decleration Of Input parameters for the Program
17 '##########################################################'
18 Def Pos PickPose
19 '##########################################################'
20 '		Decleration Of Input and Output signals
21 '##########################################################'
22 '
23 '>>>>>> INPUTS - Robot Controller <<<<<<<<'
24 Def Io PartGripped  = Bit,901     'Work piece gripped by the robot
25 Def Io FullyClosed  = Bit,902     'Gripper fully closed
26 '
27 '>>>>>> INPUTS - ProfiNet <<<<<<<<'
28 Def Io DI_RetryLastStep = Bit,132
29 Def Io DI_ExitCurrCycle = Bit,133
30 '
31 '>>>>>> OUTPUTS - ProfiNet <<<<<<<<'
32 Def Io ReturnErrorValue = Byte,164 'Return value of Error to the PLC
33 '
34 '##########################################################'
35 '		Decleration Of Local Variables
36 '##########################################################'
37 Def Inte FunErrorStatus
38 '
39 '###########################################################'
40 '       Program
41 '###########################################################'
42 Ovrd m_ProgOvrd
43 Mov P_Home
44 *RetryStep:
45  '>>>>>> Initialize the values <<<<<<<<<<'
46  FunErrorStatus% = 0
47  ReturnErrorValue = 0
48  Spd m_Fast
49  If ((DI_RetryLastStep = 1) Or (DI_ExitCurrCycle = 1)) GoTo *RetryStep
50  '>>>>>> Calculate the pick pose <<<<<<<<<<'
51  PickPose = P_ByPassPalPose
52  '
53  Mov PickPose, -100
54  Spd m_Slow
55  Mvs PickPose
56  Dly 0.5
57  CallP "GrpClose"
58  If PartGripped = 0 And FullyClosed = 1 Then
59   FunErrorStatus% = m_NoWpGrippedErr
60   CallP "GrpOpen"
61  EndIf
62  Mvs PickPose, -100
63  If (FunErrorStatus% > 0) GoTo *ErrorLable
64  '
65  '>>>>>> Return if no error found <<<<<<<<<<'
66  Spd m_Fast
67  Mov P_Home
68  GoTo *EndOfProg
69  '
70 *ErrorLable:
71  ReturnErrorValue = FunErrorStatus%
72  If ((DI_RetryLastStep = 0) And (DI_ExitCurrCycle = 0)) GoTo *ErrorLable
73  If DI_RetryLastStep = 1 GoTo *RetryStep
74  If DI_ExitCurrCycle = 1 Then
75   ReturnErrorValue = 0
76  EndIf
77 *EndOfProg:
78  m_ReturnedError = FunErrorStatus%
79 