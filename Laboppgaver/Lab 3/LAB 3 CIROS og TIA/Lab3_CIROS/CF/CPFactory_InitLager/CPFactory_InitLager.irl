PROGRAM IRL;

FROM MES_200 IMPORT ALL;

VAR
	INPUT BOOL	:	Replizieren	AT 0;
	INPUT BOOL	:	ReInit	AT 1;
	INPUT REAL	:	FachNr	AT 0;
	INPUT REAL	:	PartNr	AT 1;

VAR
	OUTPUT BOOL	:	Y_Achse_zurueck	AT 0;
	OUTPUT BOOL	:	Y_Achse_nach_vorn	AT 1;
	OUTPUT BOOL	:	Drehzylinder_0	AT 2;
	OUTPUT BOOL	:	Drehzylinder_180	AT 3;
	OUTPUT BOOL	:	LagerInitialisiert	AT 4;
	OUTPUT BOOL	:	Repliziere	AT 10;
	OUTPUT BOOL	:	ErzwingePalette	AT 11;

	OUTPUT REAL	:	Z_Achse	AT 0;
	OUTPUT REAL	:	X_Achse	AT 1;
	OUTPUT REAL	: 	AO_PartNr AT 2;
	
var
	INT : fach;
	MESFields : mes;
	
PROCEDURE moveTo (IN INT: fach);
var 
	BOOL : vorne;
	REAL : fachZInMM;
	REAL : fachXInMM;	
BEGIN
	if( fach > 0 AND fach < 90 ) then
		fach := fach -1;
		
		Y_Achse_zurueck := true;
		Y_Achse_nach_vorn := false;
		
		if( fach < 16 ) then
			vorne := true;
		else
			vorne := false;
			fach := fach - 16;
		endif;
		Drehzylinder_0 := not vorne;
		Drehzylinder_180 := vorne;

		fachZInMM := (3-(ROUND((fach / 4) -0.4))) * 140 - 5; 
		fachXInMM := (3-((fach) -(ROUND((fach / 4) -0.4)*4))) * 180 + 20; 

		Z_Achse := fachZInMM;
		X_Achse := fachXInMM;

		wait 0.0 sec;
	endif;
ENDPROC;

PROCEDURE repl (IN REAL: teilnr);
VAR	
BEGIN
	AO_PartNr := teilnr;
	ErzwingePalette := teilnr > 0;

	Repliziere		:= true;
	WAIT 0 SEC;
	Repliziere		:= false;
ENDPROC;
	
FUNCTION min( IN real : n; IN real : m ) : REAL;
BEGIN
	if( m<n ) then
		return m;
	endif;
	return( n );
ENDFCT;	
PROCEDURE initFromMES();
VAR 
	REAL : maxFach;
	REAL : maxReturn;
	REAL : currentReadFach;
	int : n;
BEGIN
	LagerInitialisiert := false;
	wait 0.1 sec;
	
	maxFach := 32;
	maxReturn := 7;
	currentReadFach := 0;
	{Automatische Initialisierung}
	if( myResourceId > 0 ) then
		fach := 1;
		while( fach <= maxFach )
			if( currentReadFach <= fach ) then
				GetPartsInBufCiros( myResourceId, 1, fach, min(maxReturn, maxFach-fach+1));
				saveMESAnswer( mes );
				
				currentReadFach := fach;
			endif;
			
			FOR n := 1 TO Round(min(maxReturn, maxFach-currentReadFach + 1))
				moveTo( fach );
				
				if (fach = 1 OR fach = 17) then 
					wait 0.5 sec; 
				endif;					
				
				repl(mes.Parameter[n]);				
				fach := fach + 1;
			ENDFOR;
		ENDWHILE;
	endif;
	LagerInitialisiert := true;
ENDPROC;

BEGIN
	initFromMES();

	fach := ROUND(fachNr);
	moveTo( fach );
	while( true )
		if( fachNr <> fach ) then
			fach := ROUND(fachNr);
			moveTo( fach );
		endif;
		if( Replizieren ) then
			repl( PartNr );
		endif;	
		
		if( ReInit ) then
			initFromMES();
		endif;
	endwhile;
ENDPROGRAM;
