PROGRAM IRL;

FROM MES_200 IMPORT ALL;
FROM RFID_100 IMPORT ALL;
FROM APPLIKATION_LABLAGER IMPORT ALL;
FROM APPLIKATIONEN IMPORT ALL;
FROM MES_COM IMPORT ALL;

PROCESS CPLab_Lager();
VAR
	INPUT BOOL	:	DI_Start 	 AT 8;
	INPUT BOOL	:	DI_Stop		 AT 9;
	INPUT BOOL	:	DI_AutoManuell	 AT 10;
	INPUT BOOL	:	DI_Richten	 AT 11;
	INPUT BOOL	:	DI_Bit0_Extra	 AT 12;
	INPUT BOOL	:	DI_SPSEnable	 AT 13;
	INPUT BOOL	:	DI_PaletteLinks	 AT 14;
	INPUT BOOL	:	DI_PaletteRechts AT 15;
	INPUT BOOL	:	DI_Bit0	AT 16;
	INPUT BOOL	:	DI_Bit1 AT 17;
	INPUT BOOL	:	DI_Bit2	AT 18;
	INPUT BOOL	:	DI_Bit3	AT 19;
	INPUT BOOL	:	DI_von_rechts		AT 20;
	INPUT BOOL	:	DI_von_links		AT 21;
	INPUT BOOL	:	DIN18_x6		AT 22;
	INPUT BOOL	:	DI_StopperGeoeffnet 	AT 23;
	
	INPUT BOOL	: 	DI_Stauueberwachung AT 90;
	INPUT BOOL	: 	DI_Bandabschaltung  AT 91;

	OUTPUT BOOL	:	DO_Start		AT 7;
	OUTPUT BOOL	:	DO_Richten		AT 8;
	OUTPUT BOOL	:	DO_Q1			AT 9;
	OUTPUT BOOL	:	DO_Q2			AT 10;
	OUTPUT BOOL	:	DO_BandRechtslauf   	AT 11;
	OUTPUT BOOL	:	DO_BandLinkslauf    	AT 12;
	OUTPUT BOOL	:	DO_BandSchleichgang 	AT 13;
	OUTPUT BOOL	:	DO_StopperOeffnen   	AT 14;
	OUTPUT BOOL	:	DOUT18_x0	AT 15;
	OUTPUT BOOL	:	DOUT18_x1	AT 16;
	OUTPUT BOOL	:	DOUT18_x2	AT 17;
	OUTPUT BOOL	:	DOUT18_x3	AT 18;
	OUTPUT BOOL	:	DO_nach_links	AT 19;
	OUTPUT BOOL	:	DO_nach_rechts	AT 20;
	OUTPUT BOOL	:	DOUT18_x6	AT 21;
	OUTPUT BOOL	:	DOUT18_x7	AT 22;
	
	RFIDFields : rfid;
	MESFields : mes;
	
	REAL : nextResourceId;	
	BOOL : firstWorkOrder;
BEGIN
	{Band einschalten}
	
	DO_BandRechtslauf := true;	
	
	{MES Zustand initialisieren}
	setStateMESMode( true );
	setStateAuto( true );
	setStateReset( false );
	setStateError( -1 );
	
	setStateBusy( true );
	labReferenzfahrt();
	setStateBusy( false );

	{Koppelsensoren einschalten, dienen vorerst nur zur Modulerkennung}
	DO_nach_links := true;
	DO_nach_rechts := true;
	
	while( true )	
		{Stopper ausfahren und auf WT warten}
		DO_StopperOeffnen := false; 
		{wait for DI_Bit0 OR DI_Bit1 OR DI_Bit2 OR DI_Bit3;}
		while( not RFIDAInventory() )
			wait 0.5 SEC;
		endwhile;

	
		nextResourceId := -1;		
		if( checkForWorkInThisStation( 1, rfid, mes, 'CPLab_Lager' ) ) then
			if( DI_Bandabschaltung ) then
				{Band stoppen}
				DO_BandRechtslauf := false;
			endif;
		
			if( myResourceId <> 0 ) then
				nextResourceId := callApp( 1, appId, rfid, mes );
			else
			{Testablauf
				mes.Parameter[1] := 90; 
				mes.Parameter[2] := 1;  
				mes.Parameter[3] := 25; 
				nextResourceId :=  App(appId, mes);
			}
			endif;
		endif;
		

		{Findet der naechste Arbeitsschritt in dieser Station statt?}
		if( myResourceId = 0 OR nextResourceId <> myResourceId ) then
			{Band starten}
			DO_BandRechtslauf := true;
			
			if( DI_Stauueberwachung ) then
				wait for not DI_PaletteRechts;
			endif;
		
			{Stopper einfahren und warten dass WT weiter gefahren ist}
			DO_StopperOeffnen := true;
			{wait for not( DI_Bit0 OR DI_Bit1 OR DI_Bit2 OR DI_Bit3 );}
			while( RFIDAInventory() )
				wait 0.1 SEC;
			endwhile;
			
			{Stopper ausfahren}
			DO_StopperOeffnen := false;
			wait for not DI_StopperGeoeffnet;
		endif;		
	endwhile;
ENDPROCESS;

PROCEDURE startGM(IN INT: gmId);
BEGIN
	CASE gmId OF
	WHEN 2: 
		RUN CPLab_Lager;
	ENDCASE;
ENDPROC;

PROCEDURE stopGM(IN INT: gmId);
BEGIN
	CASE gmId OF
	WHEN 2: 
		CANCEL CPLab_Lager;
	ENDCASE;
ENDPROC;
{
 Hauptmodul
}
PROCEDURE CPLabLager(IN INT: gmId; IN INT: _appId);
VAR
	INPUT BOOL	:	DI_SPSEnable 	 AT 13;
BEGIN
	appId := _appId;
while( true )
	WAIT FOR DI_SPSEnable;
	startGM(gmId);
	WAIT FOR NOT DI_SPSEnable;
	stopGM(gmId);
endwhile;	

ENDPROC;
ENDPROGRAM;
